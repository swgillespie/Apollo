lines = { SOI ~ line+ ~ EOI }

line = { tag_pair* ~ moves }

tag_pair = { "[" ~ identifier ~ string ~ "]" }

identifier = { ident_char+ }
ident_char = _{ 'a'..'z' | 'A'..'Z' | '0'..'9' }

string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

moves = { mov+ }
mov = { turn_number ~ san ~ san?}
san = _{ castle | normal_move }
normal_move = ${ (ASCII_ALPHA | ASCII_DIGIT | "x" | "+" | "#")+ }
castle = { "O-O-O" | "O-O" }

turn_number = { ASCII_DIGIT+ ~ "." }

WHITESPACE = _{ " " | "\n" | "\r\n" | "\t" }